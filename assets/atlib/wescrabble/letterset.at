def LetterSet := object: {
    def set;

    def add(letter){
        def x := set.get(letter);
        if: x == nil then: {x := 0};
        set.put(letter, x+1);
    };

    def init(letters){
        set := jlobby.java.util.HashMap.new();
        letters.each: &add;
    };

    def each: closure {
        def it := set.keySet().iterator();
        while: {it.hasNext()} do: {
            def l := it.next();
            def n := set.get(l);
            closure(l, n);
        };
    };

    def size(){
        def res := 0;
        each: {|_, x| res := res + x};
        res;
    };

    def pairwise(other, op){
        def copy := LetterSet.new([]);
        copy.set := set.clone();

        other.each: {|letter, otherCount|
            def myCount := copy.set.get(letter);
            if: myCount == nil then: {myCount := 0};
            copy.set.put(letter, op(myCount, otherCount));
        };
        copy;
    };

    def -(other){
        pairwise(other, {|me, you| me - you});
    };

    def +(other){
        pairwise(other, {|me, you| me + you});
    };

    def positive(){
        def res := true;
        each: {|_, count|
            if: count < 0
            then: {res := false}
        };
        res;
    };

    def repr(){ 
        set.toString();
    };

    def toList(){
        def res := jlobby.java.util.ArrayList.new();
        each: {|letter, count|
            0.to: count do: {|_| res.add(letter)};
        };
        res
    };
};

def VOWELS := "aeiouy".explode();
def CONSONANTS := "bcdfghjklmnpqrstvwxz".explode();

def max(a, b){if: a > b then: {a} else: {b}};

def randomLetters(closure, vowels := 10, consonants := 10){
    def R := jlobby.java.util.Random.new();
    0.to: max(vowels, consonants) do: {|i|
        if: i<vowels then: {closure(VOWELS[1 + R.nextInt(VOWELS.length)])};
        if: i<consonants then: {closure(CONSONANTS[1 + R.nextInt(CONSONANTS.length)])};
    }
};

self;
