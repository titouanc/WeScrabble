def makeBot(name){
    actor: {|name|
        import /.wescrabble.wescrabble;
        def log := Logger(name);

        def s := makeWeScrabble(object: {
            def getMyName(){name};
            def setAppTitle(@_){};
            def setTeam(i){log("Joined team", i)};
            def showMessage := log;
            def showMyLetters(l){log("I have the letters:" + l.toString())};
            def setBackend(_){
                _.addObserver(object:{
                    def update(){
                        def table := (0**15).map: {|row|
                            (0**15).map: {|col|
                                _.getLetterAt(row, col);
                            };
                        };
                        def repr := (table.map: {|r| r.join(" ")}).join("\n");
                        system.println();
                    };
                });
            };
            def setPlayerRack(p, r){log(p, "has rack", r)};
        });
    };
};

def T := /.at.support.timer;
def makeBots(bots, delay){
    if: bots.length > 0
    then: {
        def [name, @rest] := bots;
        makeBot(name);
        T.when: delay elapsed: {makeBots(rest, delay);}
    }
};

network.online;
makeBots(["Titou", "Cluru", "Eli", "Toon", "Denis", "Florian"], 2.seconds);
