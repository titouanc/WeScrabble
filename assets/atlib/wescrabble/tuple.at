def Placeholder := isolate: { 
    def symbol;

    def init(name){
        symbol := name;
    };

    def ==(other){true};
};

def Tuple := isolate: {
    def fields;

    def init(contents){
        fields := contents;
    };

    def at(i){
        fields[i];
    };

    def match(other){
        def match_table(t1, t2){
            if: t1.length == 0
            then: {true}
            else: {
                def [h1, @r1] := t1;
                def [h2, @r2] := t2;
                ((h1 == h2).or: {h2 == h1}).and: {match_table(r1, r2)}
            }
        };

        if: fields.length != other.fields.length
        then: {false}
        else: {match_table(fields, other.fields)};
    };
};

def publicAPI := object: {
    def remove: tup from: aList {
        def i := 0;
        def removeMeth := aList.&remove; // Grab remove(idx) method
        def it := aList.iterator();
        {|return|
            while: {it.hasNext()} do: {
                def x := it.next();
                if: tup.match(x)
                then: {
                    removeMeth.cast(jlobby.java.lang.Integer.TYPE)(i);
                    return(true);
                };
                i := i+1;
            };
            return(false);
        }.escape();
    };
    
    def tuple: contents {
        Tuple.new(contents);
    };

    def var: name {
        Placeholder.new(name);
    };
};
